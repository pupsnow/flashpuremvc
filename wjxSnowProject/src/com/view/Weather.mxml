<?xml version="1.0"?>
<!-- fds\rpc\RPCResultFaultAS.mxml -->
 <containers:MyCanvas width="400" height="321"   color="#ffffff"
    	mouseDown="startdrag(event)" mouseUp="{this.stopDrag();}" alpha="0.7"
    	xmlns:mx="http://www.adobe.com/2006/mxml" paddingTop="0" 
		creationComplete="initApp()"
		
		xmlns:containers="basecom.basecomponent.containers.*" xmlns:controls="basecom.basecomponent.controls.*">
    <mx:Script>
        <![CDATA[
            import mx.rpc.soap.WebService;
            import mx.rpc.events.ResultEvent;
            import mx.rpc.events.FaultEvent;    
            private var ws:WebService;
            private var cityName:String = "成都";
            private var intervalDuration:Number = 3000;
            private var intervalId:uint;
            private var resultArray:Array = new Array();
            [Bindable]
            private var selectIndex:int = 0;
            [Bindable]
            public var cityArray:Array = new Array();
            
            [Bindable]
            public var cards: Array = [ {label:"今天", data:1},{label:"明天", data:2}, 
                {label:"后天", data:3}];
            
            internal function startdrag(e:MouseEvent):void
            {
            	trace(e.target.name);
            	if(e.target.name=="top")
                this.startDrag();
            }
           
            public function initApp():void{
            	image2.visible = false;
        		cityCB.enabled = false;
        		dataCB.enabled = false;
            	flash.system.Security.allowDomain("http://127.0.0.1/TalkBook_asp/");
            	userAdd.text = "天气数据正在加载中... ...";
            	LoadXMLExample();
            }
            
		    public function LoadXMLExample(  ):void {
		      var loader:URLLoader = new URLLoader(  );
		      //loader.dataFormat = "TEXT";
		      loader.addEventListener( Event.COMPLETE, handleComplete );
		      loader.load( new URLRequest("com/source/city.xml") );
		    }

		    private function handleComplete( event:Event ):void {
		      try {
		        var cityXML:XML = new XML( event.target.data );
				for each(var city:XML in cityXML.cityName){
					cityArray.push(city.toString());
				}
		      } catch ( e:TypeError ) {
		        trace( "Could not parse text into XML" );
		        trace( e.message );
		      }finally {
		        getIP();
		      }
		    }
            
            public function useWebService(strArg:String):void {
        		cityCB.enabled = false;
        		dataCB.enabled = false;
                ws = new WebService();
                ws.wsdl = "http://www.webxml.com.cn/WebServices/WeatherWebService.asmx?WSDL";
                ws.getWeatherbyCityName.addEventListener("result", weatherResultHandler);
                ws.addEventListener("fault", faultHandler);
                ws.loadWSDL();
                ws.getWeatherbyCityName(strArg);
            }
            public function getIP():void{
                ws = new WebService();
                ws.wsdl = "http://www.webxml.com.cn/WebServices/IpAddressSearchWebService.asmx?WSDL";
                ws.getGeoIPContext.addEventListener("result", cityResultHandler);
                ws.addEventListener("fault", faultHandler);
                ws.loadWSDL();
                ws.getGeoIPContext();
            }

            public function cityResultHandler(event:ResultEvent):void {
            	resultArray = event.result.source;
            	var resultcName:String = resultArray[1].toString();
                //do something
                for(var i:int =0;i<cityArray.length;i++){
                	var cName:String = cityArray[i];
            		var cityRegExp:RegExp = new RegExp(cName);
            		var resultRegExp:String = cityRegExp.exec(resultcName);
            		if(resultRegExp == null){
            		}else{
            			selectIndex = i;
            			cityName = cityRegExp.exec(resultArray[1].toString());
            			useWebService(cityName);
            			cityName = "您所在城市为" + cityName;
            			userAdd.text = cityName;
            			break;
            		}
                }
                if(resultRegExp == null){
                	useWebService(cityName);
                	cityName = "您的IP地址我们无法找到,使用默认地址--成都";
                }
        	}
        	
        	public function weatherResultHandler(event:ResultEvent):void{
        		trace(event.result.toString());
        		resultArray = event.result.source;
        		data.text = resultArray[4];
        		wendu.text = resultArray[5];
        		weastatus.text = resultArray[6];
        		wend.text = resultArray[7];
        		message.text = resultArray[11];
        		image1.source="images/weather/"+resultArray[8];
        		image2.source="images/weather/"+resultArray[9];
        		cityCB.enabled = true;
        		dataCB.enabled = true;
 				intervalId = setInterval(changIcon, intervalDuration);
        	}
			
			public function changData(event:Event):void{
				var dataItem:int = event.target.selectedItem.data;
				if(dataItem == 1){
	        		data.text = resultArray[4];
	        		wendu.text = resultArray[5];
	        		weastatus.text = resultArray[6];
	        		wend.text = resultArray[7];
	        		message.text = resultArray[11];
	        		image1.source = "images/weather/"+resultArray[8];
	        		image2.source = "images/weather/"+resultArray[9];
				}else if(dataItem == 2){
	        		wendu.text = resultArray[12];
	        		weastatus.text = resultArray[13];
	        		wend.text = resultArray[14];
	        		message.text = resultArray[11];
	        		image1.source = "images/weather/"+resultArray[15];
	        		image2.source = "images/weather/"+resultArray[16];
				}else if(dataItem == 3){
	        		wendu.text = resultArray[17];
	        		weastatus.text = resultArray[18];
	        		wend.text = resultArray[19];
	        		message.text = resultArray[11];
	        		image1.source = "images/weather/"+resultArray[20];
	        		image2.source = "images/weather/"+resultArray[21];
				}
			}
			
			public function changCity(event:Event):void{
				clearInterval(intervalId); 
				var selectedCity:String = event.target.text;
				useWebService(selectedCity);
			}
			
            public function faultHandler(event:FaultEvent):void {
            	//处理错误
            }
            public function changIcon():void{
            	image1.visible = !(image1.visible);
            	image2.visible = !(image2.visible);
            }
        ]]>
    </mx:Script>  
    
    <mx:Fade id="fadeOut" duration="1000" alphaFrom="1.0" alphaTo="0.0"/>
    <mx:Fade id="fadeIn" duration="1000" alphaFrom="0.0" alphaTo="1.0"/>
    
        <mx:ApplicationControlBar  width="100%" height="40" id="top">
        	<mx:Label text="天气预报"/>
        </mx:ApplicationControlBar>
        <mx:Form x="2" y="37" width="390" height="256" paddingLeft="0" paddingRight="0" color="#ffffff">
            <mx:FormItem label="报告时间：" width="373">
                <controls:MyText width="307" id="data"/>
            </mx:FormItem>
            <mx:FormItem label="温度范围：" width="374">
                <controls:MyText width="308" id="wendu"/>
            </mx:FormItem>
            <mx:FormItem label="天气状况：" width="374">
                <controls:MyText width="308" id="weastatus"/>
            </mx:FormItem>
            <mx:FormItem label="风力情况：" width="374">
                <controls:MyText width="308" id="wend"/>
            </mx:FormItem>
            <mx:FormItem label="各项指数：" height="112" width="375">
                <controls:MyTextArea height="107" width="295" backgroundAlpha="0" id="message"/>
            </mx:FormItem>
        </mx:Form>
        <controls:MyImage width="86" height="86"
        	id="image1" hideEffect="{fadeOut}" showEffect="{fadeIn}" x="245" y="52" />
        <controls:MyImage x="245" y="52" width="86" height="86" id="image2"
        	 hideEffect="{fadeOut}" showEffect="{fadeIn}" />
        <mx:ControlBar  id="controlbar1" paddingTop="0" verticalAlign="middle" 
        	paddingBottom="0" bottom="5" width="100%">
            <controls:MyLabel text="查看的城市:"/>
            <controls:MyComboBox id="cityCB"  color="#000000" width="86" dataProvider="{cityArray}" selectedIndex="{selectIndex}" close="changCity(event)">
            </controls:MyComboBox >
            <mx:Spacer width="100%"/>
            <controls:MyLabel text="查看的时间:"/>
            <controls:MyComboBox color="#000000"   id="dataCB" width="86" dataProvider="{cards}" close="changData(event)" >
            </controls:MyComboBox >
        </mx:ControlBar>
        <mx:Label id="userAdd"  x="141" y="22"/>
</containers:MyCanvas>

